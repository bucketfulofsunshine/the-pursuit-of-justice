BACKUP ~TPoJ/backup~
SUPPORT ~bucketfulofsunshine~
VERSION ~nightly~

AUTO_TRA ~TPoJ/tra/%s~

LANGUAGE ~English~
         ~english~
         ~TPoJ/tra/english/Journal.tra~
		 ~TPoJ/tra/english/pcbarks.tra~

BEGIN ~The Pursuit of Justice~
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This mod requires BG2EE or EET~

/* CamDawg's code for State.ids */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

/* journal entries */
ADD_JOURNAL TITLE (@9000) @9001 USING ~%MOD_FOLDER%/tra/english/Journal.tra~
ADD_JOURNAL TITLE (@10000) @10001 USING ~%MOD_FOLDER%/tra/english/Journal.tra~

/* script adjustments */
// oh5300
COPY_EXISTING ~OH5300.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("OHD_Camp_hostile","oh5300",1)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~
    REPLACE_TEXTUALLY ~\(Dead("OHDTERP")\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~					  
END
BUT_ONLY

// oh5100 
COPY_EXISTING ~OH5100.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\b\(Global("OHD_journalentry","OH5100",0)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~   
    REPLACE_TEXTUALLY ~\b\(Global("OH5100_START","OH5100",0)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~ 					  
END
BUT_ONLY

// oh5120 
COPY_EXISTING ~OH5120.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\b\(Global("OHD_ritual","OH5120",0)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~   					  
END
BUT_ONLY

// oh5200 
COPY_EXISTING ~OH5200.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\b\(Global("OH5200_START","OH5200",0)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~  					  
END
BUT_ONLY

// ohdcut07
COPY_EXISTING ~ohdcut07.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\b\(True()\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~   					  
END
BUT_ONLY

// ohdcut11
COPY_EXISTING ~ohdcut11.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\b\(True()\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~   					  
END
BUT_ONLY

/* area & script extends */
EXTEND_BOTTOM ~AR0900.bcs~ ~%MOD_FOLDER%/area/AR0900.baf~
EXTEND_BOTTOM ~OH5300.bcs~ ~%MOD_FOLDER%/area/OH5300.baf~
EXTEND_BOTTOM ~OH5100.bcs~ ~%MOD_FOLDER%/area/OH5100.baf~
EXTEND_BOTTOM ~OH5200.bcs~ ~%MOD_FOLDER%/area/OH5200.baf~
EXTEND_BOTTOM ~OH5120.bcs~ ~%MOD_FOLDER%/area/OH5120.baf~
EXTEND_BOTTOM ~ohdcut07.bcs~ ~%MOD_FOLDER%/scripts/OHDCUT07.baf~
EXTEND_BOTTOM ~ohdcut11.bcs~ ~%MOD_FOLDER%/scripts/OHDCUT11.baf~

/* creature files */ 
COPY ~%MOD_FOLDER%/cre~ ~override~

/* sounds */
COPY ~%MOD_FOLDER%/sounds~ ~override~

// messenger
COPY ~%MOD_FOLDER%/cre/bfmes.cre~ ~override~
SAY NAME1 ~Messenger~
SAY NAME2 ~Messenger~

COPY ~%MOD_FOLDER%/cre/bfhsen.cre~ ~override~
SAY NAME1 ~Tennek~
SAY NAME2 ~Tennek~

/* helmite messenger spawn timer */
ACTION_FOR_EACH var IN ~AR0020~ ~AR0300~ ~AR0400~ ~AR0500~ ~AR0700~ ~AR0800~ ~AR0900~ ~AR1000~ ~AR1100~ ~AR2000~ BEGIN
  EXTEND_BOTTOM "%var%.bcs" ~%MOD_FOLDER%/timers/bfmest.baf~
END

/* scripts */ 
COMPILE ~%MOD_FOLDER%/scripts~
COMPILE ~%MOD_FOLDER%/timers~

/* dialogues */
COMPILE ~%MOD_FOLDER%/d~

/* items */
/* bollard letter */
COPY ~%MOD_FOLDER%/items/bfhlt.ITM~ ~override~
SAY 0x08 ~Letter from Bollard Firejaw~
SAY 0x0c ~Letter from Bollard Firejaw~
SAY 0x50 ~<CHARNAME>,

I write to you as a servant of Torm, and as one presently working in tandem with the faithful of Helm. Word of your valour has reached us, and we are heartened by the good deeds you have performed.

A foul murder has occurred within the halls of the Radiant Heart, one that tests the resolve of all who stand against darkness. The Helmites have requested your aid in this matter, for we believe a smaller, more discreet party may fare better than a host in confronting such evil.

Enclosed you will find directions to their camp. I trust that you will answer this call with the steadfast courage you have shown time and again.

Yours in duty and faith,
Bollard Firejaw~