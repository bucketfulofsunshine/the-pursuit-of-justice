BACKUP ~TPoJ/backup~
SUPPORT ~bucketfulofsunshine~
VERSION ~nightly~

AUTO_TRA ~TPoJ/tra/%s~

LANGUAGE ~English~
         ~english~
         ~TPoJ/tra/english/Journal.tra~

BEGIN ~The Pursuit of Justice~
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ ~This mod requires BG2EE or EET~

/* CamDawg's code for State.ids */
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

/* journal entries */
ADD_JOURNAL TITLE (@9000) @9001 USING ~%MOD_FOLDER%/tra/english/Journal.tra~

/* disable the dorn spawn & recruitment - he's the antagonist now */
EXTEND_BOTTOM ~AR0900.bcs~   ~%MOD_FOLDER%/area/AR0900.baf~

/* script adjustments */
// oh5300
COPY_EXISTING ~OH5300.BCS~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\(Global("OHD_Camp_hostile","oh5300",1)\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~
    REPLACE_TEXTUALLY ~\(Dead("OHDTERP")\)~
                      ~!Global("bfspursuitaccepted","GLOBAL",1) \1~					  
END
BUT_ONLY

/* creature files */ 
COPY ~%MOD_FOLDER%/cre~ ~override~

// messenger
COPY ~%MOD_FOLDER%/cre/bfmes.cre~ ~override~
SAY NAME1 ~Messenger~
SAY NAME2 ~Messenger~

/* helmite messenger spawn timer */
ACTION_FOR_EACH var IN ~AR0020~ ~AR0300~ ~AR0400~ ~AR0500~ ~AR0700~ ~AR0800~ ~AR0900~ ~AR1000~ ~AR1100~ ~AR2000~ BEGIN
  EXTEND_BOTTOM "%var%.bcs" ~%MOD_FOLDER%/timers/bfmest.baf~
END

/* scripts */ 
EXTEND_BOTTOM ~OH5300.bcs~ ~%MOD_FOLDER%/area/OH5300.baf~
COMPILE ~%MOD_FOLDER%/scripts~
COMPILE ~%MOD_FOLDER%/timers~

/* dialogues */
COMPILE ~%MOD_FOLDER%/d~